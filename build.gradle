plugins {
    id 'java'
}

group = 'uk.co.breadhub'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    google()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

configurations {
    external
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    external 'com.mchange:c3p0:0.9.5.5'

    // this is going to be a PHAT jar
    external group: 'mysql', name: 'mysql-connector-java', version: '8.0.15'
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    configurations.compileOnly.extendsFrom(configurations.external)
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    from sourceSets.main.output
    from {
        configurations.external.collect {
            it.isDirectory() ? it : zipTree(it).matching { exclude { it.path.contains('META-INF') } }
        }
    }
    manifest {
        attributes(
                'Title': project.name,
                'Group': project.group,
                'Version': project.version,
                'Vendor': 'braddevans',
                'Minecraft-Versions-Tested': '["1.10 to 1.16"]',
        )
    }
}